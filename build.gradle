plugins {
  id "com.github.ben-manes.versions" version "0.41.0"
  id 'com.palantir.docker' version '0.32.0'
}

apply plugin: 'java'
apply plugin: 'java-library'
apply plugin: 'application'
apply plugin: 'eclipse'

repositories {
  mavenCentral()
}

dependencies {
  implementation 'org.slf4j:slf4j-api:1.7.35'
  implementation 'com.unboundid:unboundid-ldapsdk:6.0.3'
  implementation 'org.mindrot:jbcrypt:0.4'
  implementation 'org.apache.commons:commons-lang3:3.12.0'
  implementation 'commons-io:commons-io:2.11.0'
  implementation 'com.google.guava:guava:31.0.1-jre'
  implementation 'com.beust:jcommander:1.82'
  implementation 'ch.qos.logback:logback-classic:1.2.10'
  testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
}

mainClassName = 'io.github.agebe.ldap.Ldap'

def isNonStable = { String version ->
  // added -JRE for guava
  def stableKeyword = ['RELEASE', 'FINAL', 'GA', '-JRE'].any { it -> version.toUpperCase().contains(it) }
  def regex = /^[0-9,.v-]+(-r)?$/
  return !stableKeyword && !(version ==~ regex)
}

// https://github.com/ben-manes/gradle-versions-plugin
tasks.named("dependencyUpdates").configure {
  rejectVersionIf {
    isNonStable(it.candidate.version)
  }
}

docker {
  name project.name
  tags 'latest'
  dockerfile file('Dockerfile')
  files tasks.installDist.outputs
}
